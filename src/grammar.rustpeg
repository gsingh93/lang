use super::{Program, FnDecl, Block, Stmt, Expr, BinOp, Type, ArgList, Variable, Function,
            ExternDecl, Literal};
use Expr::{BinExpr, EmptyExpr, IdentExpr, FuncCallExpr, LitExpr};
use Stmt::{DeclStmt, ExprStmt, ReturnStmt, AssignStmt, IfStmt, WhileStmt};
use Type::{IntTy, StringTy, BoolTy, VoidTy, UserTy};
use Literal::{BoolLit, NumLit, StrLit};
use BinOp::{AddOp, SubOp, MulOp, DivOp, LessOp, GreaterOp, EqualsOp};
use std::i32;

#[pub]
program -> Program
    = __ extern_decls:extern_decl* fns:function* {
        Program { extern_decls: extern_decls, fns: fns }
    }

extern_decl -> ExternDecl
    = extern_kw decl:func_decl semicolon {
        ExternDecl{ decl: decl }
    }

func_decl -> FnDecl
    = ty:type name:ident lparen args:arg_list rparen {
        FnDecl { ty: ty, name: name, args: args }
    }

function -> Function
    = decl:func_decl b:block {
        Function { decl: decl, block: b }
    }

type -> Type
    = "int" __ { IntTy } / "string" __ { StringTy } / "bool" __ { BoolTy } / "void" __ { VoidTy }
    / name:ident { UserTy(name) }

variable -> Variable
    = ty:type name:ident { Variable { ty: ty, name: name } }

arg_list -> ArgList
    = vars:variable ** comma { ArgList{ args: vars } }

block -> Block
    = lbrace s:stmt* rbrace { Block { stmts: s } }

stmt -> Stmt
    = return_stmt / assign_stmt / if_stmt / while_stmt / decl_stmt / expr_stmt

decl_stmt -> Stmt
    = var:variable equals e:expr semicolon {
        DeclStmt(var, e)
    }

expr_stmt -> Stmt
    = e:expr semicolon {
        ExprStmt(e)
    }

return_stmt -> Stmt
    = return_kw e:expr semicolon {
        ReturnStmt(e)
    }

assign_stmt -> Stmt
    = name:ident equals e:expr semicolon {
        AssignStmt(name, e)
    }

if_stmt -> Stmt
    = if_kw e:expr b1:block b2:(else_kw b:block { b })? { IfStmt(e, b1, b2)}

while_stmt -> Stmt
    = while_kw e:expr b:block { WhileStmt(e, b) }

expr -> Expr
    = t:term e:bin_expr_ {
        match e {
            EmptyExpr => t,
            BinExpr(box EmptyExpr, op, e2) => BinExpr(Box::new(t), op, e2),
            _ => panic!("Got unexpected expression {:?}", e)
        }
    }

bin_expr_ -> Expr
    = bin_expr / __ { EmptyExpr }

bin_expr -> Expr
    = op:bin_op e2:expr __ {
        BinExpr(Box::new(EmptyExpr), op, Box::new(e2))
    }

term -> Expr
    = true_kw { LitExpr(BoolLit(true)) } / false_kw { LitExpr(BoolLit(false)) }
    / l:num { LitExpr(l) } / l:str_lit { LitExpr(l) } / name:ident e:func_or_ident_expr {
        match e {
            EmptyExpr => IdentExpr(name),
            FuncCallExpr(_, params) => FuncCallExpr(name, params),
            _ => panic!("Got unexpected expression {:?}", e)
        }
    }

func_or_ident_expr -> Expr
    = lparen params:param_list rparen { FuncCallExpr("".to_string(), params) }
    / __ { EmptyExpr }

param_list -> Vec<Expr>
    = expr ** comma

str_lit -> Literal
    = quote s:([^"]* { match_str.to_owned() }) quote { StrLit(s) }

num -> Literal
    = n:([0-9]+ { match_str.parse().unwrap() }) __ {
        if n > i32::MAX {
            panic!("Integer literal {} too large", n)
        }
        NumLit(n)
    }

bin_op -> BinOp
    = add_tok { AddOp } / sub_tok { SubOp } / mul_tok { MulOp } / div_tok { DivOp }
    / lt_tok { LessOp } / gt_tok { GreaterOp } / eq_tok { EqualsOp }

ident_char -> char
    = [a-zA-Z0-9_] { match_str.char_at(0) }

ident -> String
    = name:(letter ident_char* { match_str.to_owned() }) __ { name }

letter -> char
    = [a-zA-Z] { match_str.char_at(0) }

add_tok = '+' __
sub_tok = '-' __
mul_tok = '*' __
div_tok = '/' __
lt_tok = '<' __
gt_tok = '>' __
eq_tok = '==' __
return_kw = "return" kw__
extern_kw = "extern" kw__
true_kw = "true" kw__
false_kw = "false" kw__
if_kw = "if" kw__
else_kw = "else" kw__
while_kw = "while" kw__
kw__ = (!ident_char) __

semicolon = ';' __
lparen = '(' __
rparen = ')' __
lbrace = '{' __
rbrace = '}' __
equals = '=' __
comma = ',' __
quote = '"'

__ = (whitespace / eol / comment)*

comment
  = singleLineComment
  / multiLineComment

singleLineComment
  = "//" (!eolChar .)*

multiLineComment
  = "/*" (!"*/" .)* "*/"

eol
  = "\n"
  / "\r\n"
  / "\r"

eolChar
  = [\n\r]

whitespace
  = [ \t]
